/*
 * Copyright (c) 2020 Ubique Innovation AG <https://www.ubique.ch>
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * SPDX-License-Identifier: MPL-2.0
 */

import com.diffplug.spotless.LineEnding

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
    id 'com.google.protobuf'
    id 'com.diffplug.spotless' version '5.7.0'
}
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 26
        versionName "0.2.6"
        testInstrumentationRunnerArgument 'androidx.benchmark.suppressErrors', 'EMULATOR,LOW-BATTERY,ACTIVITY-MISSING,DEBUGGABLE,UNLOCKED,UNSUSTAINED-ACTIVITY-MISSING'
        testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"

        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
    }

    defaultPublishConfig "release"

    flavorDimensions "version"

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

protobuf {
    // The following lines are copied from the build.gradle file of the Server-Protocol-Buffer
    // project inside the CoraLibre-Android / Corona-Warn app repository:

    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }

    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

spotless {
    kotlin {
        target "src/*/java/**/*.kt"
        ktlint()
        lineEndings = LineEnding.UNIX
        endWithNewline()
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = System.getenv('GROUP')
                artifactId = System.getenv('ARTIFACT')
                version = System.getenv('VERSION')
            }
        }
    }
}

ext.readProperty = { paramName ->
    if (project.hasProperty(paramName)) {
        return project.getProperties().get(paramName)
    } else {
        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
        }
        return properties.getProperty(paramName)
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation('androidx.security:security-crypto:1.0.0-rc02') {
        exclude group: 'com.google.protobuf', module: "protobuf-javalite"
        // See:
        // https://stackoverflow.com/questions/61518039/androidx-security-with-google-protobuf
    }
    implementation 'androidx.work:work-runtime-ktx:2.3.4'

    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation('com.squareup.retrofit2:converter-protobuf:2.8.1') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    // I changed the protobuf lite version to resolve a duplicate class conflict when building
    // the CoraLibre App. This version is the same as in the original corona warn app. Also
    // see the 'protobuf {...}' section above.

    implementation 'com.google.crypto.tink:tink-android:1.4.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.benchmark:benchmark-junit4:1.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'org.mockito:mockito-android:2.17.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"


    // PERSISTENCE (based on CWA build.gradle to avoid version conflicts)

    // Also see:
    // https://developer.android.com/training/data-storage/room/#java
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"


}
